plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.5.10'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group = project.mvn_group
version = project.console_version
if (project.build_number != "undefined") version += "+build.${project.build_number}"

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib"
    api 'net.mamoe:mirai-core:2.7.1'
    api 'org.apache.logging.log4j:log4j-api:2.14.1'
    api 'org.apache.logging.log4j:log4j-core:2.14.1'
    api 'com.google.code.gson:gson:2.8.9'
    api "com.github.HyDevelop:HyConfigLib:3.1.52"
    api 'org.slf4j:slf4j-nop:1.7.32'
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
}

task copyJar(type: Copy) {
    from configurations.runtimeClasspath
    into 'build/libs/lib'
}

jar.dependsOn([copyJar])

jar {
    from("LICENSE")
    manifest {
        attributes ([
                "Main-Class" : "net.cjsah.console.MainKt",
                "Class-Path" : configurations.runtimeClasspath.files.collect { "lib/$it.name" }.join(' ')
        ])
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task docJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

tasks.withType(Javadoc) {
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
//          version SemVer_version
// 			groupId project.group
            artifactId project.archivesBaseName
            from components.java
            artifact sourcesJar
            artifact docJar
        }
    }

    def ENV = System.getenv()
    if (ENV.MAVEN_URL) {
        repositories.maven {
            url ENV.MAVEN_URL
            credentials {
                username ENV.MAVEN_USERNAME
                password ENV.MAVEN_PASSWORD
            }
        }
    }
}
